table _measure
	lineageTag: 1919ba0b-d570-4037-82be-76d2c015881f

	measure '# of Employee' = COUNTROWS(fact_employee)
		formatString: 0
		displayFolder: Diversity
		lineageTag: 9870eb51-35fb-4b39-9840-2c0837313005

	measure '# of Active Employees' = ```
			
			COUNTROWS(
			    FILTER(fact_employee, 
			           fact_employee[Current Employment Status]="Active")
			)
			```
		formatString: 0
		displayFolder: Diversity
		lineageTag: e8373169-50ef-4d74-9667-691d447c048f

	measure '# of Female' = ```
			
			COUNTROWS(
			    FILTER(fact_employee, 
			           fact_employee[Gender]="Female")
			)
			```
		formatString: 0
		displayFolder: Diversity
		lineageTag: 506a76a0-5f3f-4291-87ec-4b28b5f688dd

	measure '# of Male' = ```
			
			COUNTROWS(
			    FILTER(fact_employee, 
			           fact_employee[Gender]="Male")
			)
			```
		formatString: 0
		displayFolder: Diversity
		lineageTag: 926be376-df37-48f0-8df5-15d7db0c829a

	measure '# of Other' = ```
			
			COUNTROWS(
			    FILTER(fact_employee, 
			           fact_employee[Gender]="Other")
			)
			```
		formatString: 0
		displayFolder: Diversity
		lineageTag: dacf613e-76b6-4526-9827-c849c6617c4f

	measure %Female =
			
			DIVIDE([# of Female], [# of Employee],0)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Diversity
		lineageTag: 209be2b2-f63e-4b3a-b492-ba463925effa

	measure %Male =
			
			DIVIDE([# of Male], [# of Employee],0)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Diversity
		lineageTag: bbb8b413-088e-4fab-8edb-4c80170b00e9

	/// No of total Resignation
	measure '# of Resignation' =
			
			CALCULATE(
			    COUNTROWS(fact_employee),
			    FILTER(
			        fact_resigned,
			        fact_resigned[Employee ID]
			    )
			)
		formatString: 0
		displayFolder: Resigned
		lineageTag: 981b2ca8-d485-4fa9-b8e6-a92d8fb1ea49

	/// No of total Voluntary Resignation
	measure '# of Voluntary Resignation' =
			
			CALCULATE(
			    COUNTROWS(fact_employee),
			    FILTER(
			        fact_resigned,
			        fact_resigned[Resigned Type] = "Voluntary"
			    )
			)
		formatString: 0
		displayFolder: Resigned\resigned profile
		lineageTag: d7dde708-1d10-4eaf-8d13-525fa442e096

	/// No of total involuntary Resignation
	measure '# of Involuntary Resignation' =
			
			CALCULATE(
			    COUNTROWS(fact_employee),
			    FILTER(
			        fact_resigned,
			        fact_resigned[Resigned Type] = "Involuntary"
			    )
			)
		formatString: 0
		displayFolder: Resigned\resigned profile
		lineageTag: c1efdaba-db69-4d67-952c-5c7b216ba352

	measure '# of First Year Resignation' =
			
			CALCULATE(
			    [# of Resignation],
			    FILTER(
			        fact_resigned,
			        DATEDIFF(
			            RELATED(fact_employee[Hire Date]),
			            fact_resigned[Resigned Date],
			            YEAR
			        ) <= 1
			    )
			)
		formatString: 0
		displayFolder: Resigned\resigned profile
		lineageTag: 11c7d296-4291-44ff-a64a-4ca7673dcd4f

	measure '% Voluntary Resignation' = DIVIDE([# of Voluntary Resignation], [# of Resignation],0)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Resigned\resigned profile
		lineageTag: 21c09dcd-e770-4cdb-b271-984afb91d2a8

	measure '% Involuntary Resignation' = DIVIDE([# of Involuntary Resignation], [# of Resignation],0)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Resigned\resigned profile
		lineageTag: 1cc5baec-64cc-41e3-9afe-6d02b3c759f2

	measure '% First Year Resignation' = DIVIDE([# of First Year Resignation], [# of Resignation],0)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Resigned\resigned profile
		lineageTag: a7cb0e4c-737e-4b50-9d5a-d88fa1bc5202

	measure '# of Resignation by Reason' =
			
			COUNTROWS(fact_resigned)
		formatString: 0
		displayFolder: Resigned\resigned profile\reason bar chart
		lineageTag: 0235a7e8-5ed8-4152-8f5e-a2013616d94c

	measure 'Labels PlaceHolders' = 0
		formatString: 0
		displayFolder: Resigned\resigned profile\reason bar chart
		lineageTag: 51e6c791-6ce0-452d-80da-e8f66f0bebad

	measure 'Label Bar Chart- Resignation Reason' =
			
			SELECTEDVALUE(dim_resignationReason[Reason Description])
		displayFolder: Resigned\resigned profile\reason bar chart
		lineageTag: e2065e24-dd1f-473e-82a3-595d70dd506c

	measure 'Avg Resignation Per Month' =
			
			VAR _Resignation= [# of Resignation]
			Var _MonthCount=
			DATEDIFF(
			    Date( 2018,1,1),
			    TODAY(),
			    MONTH
			) +1
			RETURN
			DIVIDE(
			    _Resignation,
			    _MonthCount,
			    0
			)
		displayFolder: Resigned\Resigned Analysis
		lineageTag: be15f5ee-d5a7-4d92-a132-12d6ef44420c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Avg Time to Resign (Years)' =
			
			AVERAGEX(
			    fact_resigned,
			    DATEDIFF(
			        RELATED(fact_employee[Hire Date]),
			        fact_resigned[Resigned Date],
			        YEAR
			    )
			)
		displayFolder: Resigned\Resigned Analysis
		lineageTag: cf383107-93c3-4f03-873c-3d2373129167

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure #male = [# of Male]*-1
		formatString: 0
		displayFolder: Resigned\Resigned Analysis
		lineageTag: d1f3c617-be48-47e0-96a7-2c4f7758351b

	measure #female = [# of Female]*-1
		formatString: 0
		displayFolder: Resigned\Resigned Analysis
		lineageTag: 95daf59e-58c7-4b41-b1f0-3ce358895b7e

	measure '# of Resignation PY' =
			
			CALCULATE(
			    [# of Resignation],
			    YEAR(fact_resigned[Resigned Date]) = YEAR(TODAY()) - 1
			)
		formatString: 0
		displayFolder: Resigned\Resigned Analysis
		lineageTag: 79ed1f5d-a625-4536-b5c7-dd9bd2a8fec7

	measure '# of Resignation CY' = ```
			
			CALCULATE(
			    [# of Resignation],
			    YEAR(fact_resigned[Resigned Date]) = YEAR(TODAY())
			)
			
			```
		formatString: 0
		displayFolder: Resigned\Resigned Analysis
		lineageTag: b90eb4a3-8b7b-4391-beba-fe8ba891829b

	measure 'New Hires' = COUNTROWS(fact_employee)
		formatString: 0
		displayFolder: Resigned\Resigned Analysis
		lineageTag: 924cfdee-8c95-4737-8951-a2063265c345

	measure 'Overall Attrition Rate' = ```
			
			DIVIDE(
			    [# of Resignation], 
			    [# of Employee], 
			    0
			)
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Resigned\Attrition Analysis
		lineageTag: 2f02b0a0-f0ec-46ad-bed8-9b4ad0969652

	measure 'Voluntary Attrition Rate' =
			
			DIVIDE(
			    [# of Voluntary Resignation],
			    [# of Employee],
			    0
			)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Resigned\Attrition Analysis
		lineageTag: 7cf070bb-d849-4dff-b902-9d098c918b44

	measure 'Involuntary Attrition Rate' =
			
			DIVIDE(
			    [# of Involuntary Resignation],
			    [# of Employee],
			    0
			)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Resigned\Attrition Analysis
		lineageTag: df55039d-15aa-4c59-826f-09a5ca3f380e

	measure 'Ethnicity Count' = COUNTROWS(fact_employee)
		formatString: 0
		lineageTag: bcf08fb4-80d4-4f49-9411-cca7f800042e

	measure Hires = ```
			
			CALCULATE(
			    COUNT(fact_employee[Employee ID]),
			    fact_employee[Hire Date] >= MIN('Date'[Date]),
			    fact_employee[Hire Date] <= MAX('Date'[Date])
			)
			
			```
		formatString: 0
		lineageTag: 30425783-3171-485d-99ea-822a02d149ce

	measure Terminations = ```
			
			CALCULATE(
			    COUNT(fact_employee[Employee ID]),
			    fact_employee[Current Employment Status] = "Resigned" || 
			    fact_employee[Current Employment Status] = "Terminated"
			)
			```
		formatString: 0
		lineageTag: 245c7528-abf1-4a2b-9424-f2e06c8be702

	measure 'Net Change in Headcount' = ```
			
			[Hires] - [Terminations]
			
			```
		formatString: 0
		lineageTag: 3085b68f-6b86-480b-8af2-e0672147aff8

	measure 'Overtime Headcount' = ```
			
			CALCULATE(
			    COUNT(fact_performance[Employee ID]),
			    fact_performance[Overtime] > 0
			)
			
			```
		formatString: 0
		lineageTag: 0a1239a6-0b9f-45a8-96d6-ff524acb60d6

	measure 'Resignation Rate by Age Band' = ```
			
			CALCULATE([Overall Attrition Rate], 
			    GROUPBY(fact_employee, fact_employee[Age Banding]))
			
			```
		displayFolder: Resigned\Attrition Analysis
		lineageTag: c9b73368-e6d2-4895-983d-595301f97f7d

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Categorical Score' =
			
			VAR DepartmentScore = CALCULATE(DIVIDE([# of Resignation], [# of Employee], 0), ALLEXCEPT(fact_employee, fact_employee[Department]))
			VAR JobTitleScore = CALCULATE(DIVIDE([# of Resignation], [# of Employee], 0), ALLEXCEPT(fact_employee, fact_employee[Job Title]))
			VAR LocationScore = CALCULATE(DIVIDE([# of Resignation], [# of Employee], 0), ALLEXCEPT(fact_employee, fact_employee[Country]))
			VAR EthnicityScore = CALCULATE(DIVIDE([# of Resignation], [# of Employee], 0), ALLEXCEPT(fact_employee, fact_employee[Ethnicity]))
			VAR OrgLevelScore = CALCULATE(DIVIDE([# of Resignation], [# of Employee], 0), ALLEXCEPT(fact_employee, fact_employee[Organization Level]))
			VAR EducationScore = CALCULATE(DIVIDE([# of Resignation], [# of Employee], 0), ALLEXCEPT(fact_employee, fact_employee[Education Level]))
			VAR GenderScore = CALCULATE(DIVIDE([# of Resignation], [# of Employee], 0), ALLEXCEPT(fact_employee, fact_employee[Gender]))
			VAR EmpStatusScore = CALCULATE(DIVIDE([# of Resignation], [# of Employee], 0), ALLEXCEPT(fact_employee, fact_employee[Employment Status]))
			VAR EmpTypeScore = CALCULATE(DIVIDE([# of Resignation], [# of Employee], 0), ALLEXCEPT(fact_employee, fact_employee[Employment Type]))
			RETURN
			(DepartmentScore + JobTitleScore + LocationScore + EthnicityScore + OrgLevelScore + EducationScore + GenderScore + EmpStatusScore + EmpTypeScore) / 9 * 0.4
		displayFolder: Resigned\Attrition Prediction
		lineageTag: 2aab0826-ed03-4d45-8d97-2da6fc534dc3

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Numerical Score' = ```
			
			VAR PromotionScore = 1 - DIVIDE(
			    SUM(fact_employee[Times Promoted]), 
			    CALCULATE(MAX(fact_employee[Times Promoted]), ALL(fact_employee)), 
			    0
			)
			
			VAR TenureScore = SWITCH(
			    SELECTEDVALUE(fact_employee[tenure_band]),
			    "<1 Year", 1,
			    "1-3 Years", 0.7,
			    "3-6 Years", 0.4,
			    "6+ Years", 0.2,
			    0 // Default case
			)
			
			VAR MgmtExpScore = 1 - DIVIDE(
			    AVERAGE(fact_resigned[Management/Team Experience Score]), 
			    5, 
			    0
			)
			
			VAR SatisfactionScore = 1 - DIVIDE(
			    AVERAGE(exit_interview_survey[Work Environment Satisfaction Score]), 
			    5, 
			    0
			)
			
			VAR TeamSizeScore = DIVIDE(
			    AVERAGE(fact_performance[Team Size]), 
			    CALCULATE(MAX(fact_performance[Team Size]), ALL(fact_performance)), 
			    0
			)
			
			VAR WorkingHoursScore = DIVIDE(
			    AVERAGE(fact_performance[Working Hours]), 
			    CALCULATE(MAX(fact_performance[Working Hours]), ALL(fact_performance)), 
			    0
			)
			
			VAR ProjectScore = DIVIDE(
			    SUM(fact_performance[Projects Handled]), 
			    SUM(fact_performance[Project Target]), 
			    0
			)
			
			VAR IncentiveScore = 1 - DIVIDE(
			    SUM(fact_performance[Incentive Amount ($)]), 
			    CALCULATE(MAX(fact_performance[Incentive Amount ($)]), ALL(fact_performance)), 
			    0
			)
			
			 VAR BonusScore = 1 - DIVIDE(
			    SUM(fact_performance[Bonus ($)]), 
			    CALCULATE(MAX(fact_performance[Bonus ($)]), ALL(fact_performance)), 
			    0
			)
			
			RETURN
			(PromotionScore + TenureScore + MgmtExpScore + SatisfactionScore + 
			 TeamSizeScore + WorkingHoursScore + ProjectScore + IncentiveScore + 
			 BonusScore) / 9 * 0.6
			```
		displayFolder: Resigned\Attrition Prediction
		lineageTag: 32540739-3149-4d0c-abb0-926324f31006

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Risk Score' = [Categorical Score] + [Numerical Score]
		displayFolder: Resigned\Attrition Prediction
		lineageTag: f0a4adb5-2455-4f60-9382-94a793fa58e1

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Resignation Probability' = ```
			
			VAR CurrentEmpID = SELECTEDVALUE(fact_employee[Employee ID])
			VAR CurrentStatus = SELECTEDVALUE(fact_employee[Current Employment Status])
			VAR MaxScore = 1
			VAR MinScore = 0
			RETURN
			IF(
			    AND(
			        NOT(ISBLANK([Risk Score])),
			        CurrentStatus = "Active"
			    ),
			    MIN(MAX(DIVIDE([Risk Score] - MinScore, MaxScore - MinScore) * 100, 0), 100),
			    IF(
			        CurrentStatus = "Resigned",
			        BLANK(),
			        0
			    )
			)
			
			```
		displayFolder: Resigned\Attrition Analysis
		lineageTag: 8272abde-5277-43c6-aea9-cbc6e1ba0e1c

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'Risk Category' =
			
			SWITCH(
			    TRUE(),
			    [Resignation Probability] >= 75, "Very High Risk",
			    [Resignation Probability] >= 50, "High Risk",
			    [Resignation Probability] >= 25, "Medium Risk",
			    [Resignation Probability] >= 0, "Low Risk",
			    "Unknown"
			)
		displayFolder: Resigned\Attrition Prediction
		lineageTag: 935a708b-4aac-4d4c-aeb8-28a10b669979

	measure 'Very High Risk Employee' =
			
			CALCULATE(
			    COUNTROWS(fact_employee),
			    fact_employee[Risk Category]="Very High Risk" && fact_employee[Current Employment Status]="Active"
			)
		formatString: 0
		displayFolder: Resigned\Attrition Prediction
		lineageTag: 93de24d2-2e23-49d5-903a-6134db67390b

	measure 'High Risk Employee' =
			
			CALCULATE(
			    COUNTROWS(fact_employee),
			    fact_employee[Risk Category]="High Risk" && fact_employee[Current Employment Status]="Active"
			)
		formatString: 0
		displayFolder: Resigned\Attrition Prediction
		lineageTag: 69d23417-fc81-4d0d-b70f-3d2326604d4d

	measure 'Medium Risk Employee' =
			
			CALCULATE(
			    COUNTROWS(fact_employee),
			    fact_employee[Risk Category]="Medium Risk"
			)
		formatString: 0
		lineageTag: 29ea97dd-0d88-420d-818a-a762b1b197ef

	measure 'Low Risk Employee' =
			
			CALCULATE(
			    COUNTROWS(fact_employee),
			    fact_employee[Risk Category]="Low Risk" && fact_employee[Current Employment Status]="Active"
			)
		formatString: 0
		displayFolder: Resigned\Attrition Prediction
		lineageTag: 17789e82-490d-4f9c-847d-c52e7af0bc47

	measure 'True Positives' =
			
			CALCULATE(
			    COUNTROWS(fact_employee),
			    fact_employee[Current Employment Status] = "Resigned",
			    fact_employee[Resignation Probability] >= 30
			)
		formatString: 0
		displayFolder: Resigned\Attrition Prediction
		lineageTag: 7998f9a6-a09a-4210-9bf3-3667dc3475f9

	measure 'False Positives' =
			
			CALCULATE(
			    COUNTROWS(fact_employee),
			    FILTER(
			        fact_employee,
			        fact_employee[Current Employment Status] = "Active" &&
			        [Resignation Probability] >= 30
			    )
			)
		formatString: 0
		displayFolder: Resigned\Attrition Prediction
		lineageTag: 9aa06805-fb88-467c-9d07-f97f22486bb5

	measure 'False Negetives' =
			
			CALCULATE(
			    COUNTROWS(fact_employee),
			    FILTER(
			        fact_employee,
			        fact_employee[Current Employment Status] = "Resigned" &&
			        [Resignation Probability] < 30
			    )
			)
		formatString: 0
		displayFolder: Resigned\Attrition Prediction
		lineageTag: 311a161b-2249-44ca-8ba1-0a9c4eb6aca6

	measure Precision =
			
			DIVIDE(
			    [True Positives],
			    [True Positives] + [False Positives],
			    0
			)
		displayFolder: Resigned\Attrition Prediction
		lineageTag: 2965059d-521b-4f6e-80d7-9a9ca91fefea

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure Recall =
			
			DIVIDE(
			    [True Positives],
			    [True Positives] + [False Negetives],
			    0
			)
		displayFolder: Resigned\Attrition Prediction
		lineageTag: 0aab47b2-073a-4086-be32-51d4384abc30

		annotation PBI_FormatHint = {"isGeneralNumber":true}

	measure 'F1 Score' =
			
			VAR _Precision = [Precision]
			VAR _Recall = [Recall]
			RETURN
			2 * DIVIDE(_Precision * _Recall, _Precision + _Recall, 0)
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Resigned\Attrition Prediction
		lineageTag: 7dea056a-e904-4462-8bfb-d2360d0e7f1c

	measure 'F1 Score Not Resigned' = ```
			
			VAR _TruePositives = 
			    CALCULATE(
			        [True Positives],
			        fact_employee[Current Employment Status] = "Active"
			    )
			VAR _FalsePositives = 
			    CALCULATE(
			        [False Positives],
			        fact_employee[Current Employment Status] = "Active"
			    )
			VAR _FalseNegatives = 
			    CALCULATE(
			        [False Negetives],
			        fact_employee[Current Employment Status] = "Active"
			    )
			VAR _Precision = DIVIDE(_TruePositives, _TruePositives + _FalsePositives, 0)
			VAR _Recall = DIVIDE(_TruePositives, _TruePositives + _FalseNegatives, 0)
			RETURN
			2 * DIVIDE(_Precision * _Recall, _Precision + _Recall, 0)
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Resigned\Attrition Analysis
		lineageTag: 283f1abf-2835-457c-ab0e-0af5fe9964de

	measure 'F1 Score Resigned' = ```
			
			VAR _TruePositives = 
			    CALCULATE(
			        [True Positives],
			        fact_employee[Current Employment Status] = "Resigned"
			    )
			VAR _FalsePositives = 
			    CALCULATE(
			        [False Positives],
			        fact_employee[Current Employment Status] = "Resigned"
			    )
			VAR _FalseNegatives = 
			    CALCULATE(
			        [False Negetives],
			        fact_employee[Current Employment Status] = "Resigned"
			    )
			VAR _Precision = DIVIDE(_TruePositives, _TruePositives + _FalsePositives, 0)
			VAR _Recall = DIVIDE(_TruePositives, _TruePositives + _FalseNegatives, 0)
			RETURN
			2 * DIVIDE(_Precision * _Recall, _Precision + _Recall, 0)
			
			```
		formatString: 0.00%;-0.00%;0.00%
		displayFolder: Resigned\Attrition Analysis
		lineageTag: ffb3e192-ba08-430f-8c08-93bd9d4c5f1a

	partition _measure = m
		mode: import
		source =
				let
				    Source = Table.FromRows(Json.Document(Binary.Decompress(Binary.FromText("i44FAA==", BinaryEncoding.Base64), Compression.Deflate)), let _t = ((type nullable text) meta [Serialized.Text = true]) in type table [Column1 = _t]),
				    #"Changed Type" = Table.TransformColumnTypes(Source,{{"Column1", type text}}),
				    #"Removed Columns" = Table.RemoveColumns(#"Changed Type",{"Column1"})
				in
				    #"Removed Columns"

	annotation PBI_ResultType = Table

	annotation PBI_NavigationStepName = Navigation

